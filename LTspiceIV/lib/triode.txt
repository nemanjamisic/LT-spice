

.SUBCKT TRIODE A G K H1 H2
+PARAMS: RCO=1.6 RHO=10.5 HTV=6.3 HWU=10.5
+ LIP=1 LIF=3.7E-3 RAF=18E-3 RAS=1 CDO=0 RAP=4E-3
+ ERP=1.5 
+ MU0=17.3 MUR=19E-3 EMC=9.6E-6 GCO=0 GCF=213E-6
+ CGA=3.9p CGK=2.4p CAK=0.7p 

Rcool H1 HA {RCO}
Rload HA HB 1M
Esens HD 0  VALUE {V(HA,HB)*1000}
Epwr  HE 0  VALUE {V(H1,H2)*V(HD)/(PWR({HTV},2)/{RHO})}
RH1   HE HF 91k
CH1   HF 0  {HWU/1E6}
EH2   HG 0  VALUE {V(HF)}
RH2   HG HH 270k
CH2   HH 0  {HWU/1E6}
EH3   HJ 0  VALUE {LIMIT(V(HH)-0.75,0,1E6)*4}
RH3   HJ HK 91k
CH3   HK 0  {HWU/1E6}
Ghot  HB H2 VALUE {(1/(V(HG)+0.001))/({RHO}-{RCO})*V(HB,H2)}
Elim  LI 0  VALUE {PWR(LIMIT(V(A,K),0,1E6),{LIP})*{LIF}}
Egg   GG 0  VALUE {V(G,K)-{CDO}}
Erpf  RP 0  VALUE {1-PWR(LIMIT(-V(GG)*{RAF},0,0.999),{RAS})+LIMIT(V(GG),0,1E6)*{RAP}}
Egr   GR 0  VALUE {LIMIT(V(GG),0,1E6)+LIMIT((V(GG))*(1+V(GG)*{MUR}),0,-1E6)}
Eem   EM 0  VALUE {LIMIT(V(A,K)+V(GR)*{MU0},0,1E6)}
Eep   EP 0  VALUE {PWR(V(EM),ERP)*{EMC}*V(RP)}
Eel   EL 0  VALUE {LIMIT(V(EP),0,V(LI))}
Eld   LD 0  VALUE {LIMIT(V(EP)-V(LI),0,1E6)}
Ga    A  K  VALUE {V(HK)*V(EL)}
Egf   GF 0  VALUE {PWR(LIMIT(V(G,K)-{GCO},0,1E6),1.5)*{GCF}}
Gg    G  K  VALUE {(V(GF)+V(LD))*V(HK)}
CM1	G	K	{CGK}
CM2	A	G	{CGA}
CM3	A	K	{CAK}
RF1	A	0	1000MEG
RF2	G	0	1000MEG
RF3	K	0	1000MEG
.ENDS

.SUBCKT 6SN7_GTB A G K H1 H2
XV1 A G K H1 H2 TRIODE
+PARAMS: RCO= 3.2 RHO= 21 HTV= 6.3 HWU= 10.5 
+ LIP= 1 LIF= 0.0037 RAF= 0.02 RAS= 2 CDO= 0 
+ RAP= 0.002 ERP= 1.4 
+ MU0= 19.2642 MUR= 0.006167 EMC= 0.0000189 
+ GCO= 0 GCF= 0.000213 
+ CGA=3.90E-12 CGK=2.40E-12 CAK=7.00E-13
.ENDS

.SUBCKT 12AT7 A G K H1 H2
XV1 A G K H1 H2 TRIODE
+PARAMS: RCO= 6.2 RHO= 42 HTV= 6.3 HWU= 10.5 
+ LIP= 1 LIF= 0.0037 RAF= 0.09869 RAS= 1 CDO=-0.5 
+ RAP= 0.1 ERP= 1.4 
+ MU0= 45.093 MUR= 0.012937 EMC= 0.00000863 
+ GCO=-0.5 GCF= 0.00012 
+ CGA=1.60E-12 CGK=2.30E-12 CAK=4.00E-13
.ENDS

.SUBCKT 12AU7 A G K H1 H2
XV1 A G K H1 H2 TRIODE
+PARAMS: RCO= 6.2 RHO= 42 HTV= 6.3 HWU= 10.5 
+ LIP= 1 LIF= 0.0037 RAF= 0.024778659 RAS= 2.040491735 CDO= 0 
+ RAP= 0.18 ERP= 1.35 
+ MU0= 14.27427 MUR= 0.005857103 EMC= 0.0000236 
+ GCO= 0 GCF= 0.00012 
+ CGA=1.60E-12 CGK=1.80E-12 CAK=4.50E-13
.ENDS

.SUBCKT 12AX7 A G K H1 H2
XV1 A G K H1 H2 TRIODE
+PARAMS: RCO= 6.2 RHO= 42 HTV= 6.3 HWU= 10.5 
+ LIP= 1.5 LIF= 0.000016 RAF= 0.076498 RAS= 1 CDO=-0.53056 
+ RAP= 0.18 ERP= 1.5 
+ MU0= 87.302 MUR=-0.013621 EMC= 0.00000111 
+ GCO=-0.2 GCF= 0.00001 
+ CGA=3.90E-12 CGK=2.40E-12 CAK=7.00E-13
.ENDS

