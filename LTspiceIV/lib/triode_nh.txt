
.PARAM EMISSIONRANGE = 1

.SUBCKT TRIODENH A G K
+PARAMS: LIP=1 LIF=3.7E-3 RAF=18E-3 RAS=1 CDO=0 RAP=4E-3
+ ERP=1.5
+ MU0=17.3 MUR=19E-3 EMC=9.6E-6 GCO=0 GCF=213E-6
+ CGA=3.9p CGK=2.4p CAK=0.7p 

Elim  LI 0  VALUE {PWR(LIMIT(V(A,K),0,1E6),{LIP})*{LIF}}
Egg   GG 0  VALUE {V(G,K)-{CDO}}
Erpf  RP 0  VALUE {1-PWR(LIMIT(-V(GG)*{RAF},0,0.999),{RAS})+LIMIT(V(GG),0,1E6)*{RAP}}
Egr   GR 0  VALUE {LIMIT(V(GG),0,1E6)+LIMIT((V(GG))*(1+V(GG)*{MUR}),0,-1E6)}
Eem   EM 0  VALUE {LIMIT(V(A,K)+V(GR)*{MU0},0,1E6)}
Eep   EP 0  VALUE {PWR(V(EM),ERP)*{EMC}*{EMISSIONRANGE}*V(RP)}
Eel   EL 0  VALUE {LIMIT(V(EP),0,V(LI))}
Eld   LD 0  VALUE {LIMIT(V(EP)-V(LI),0,1E6)}
Ga    A  K  VALUE {V(EL)}
Egf   GF 0  VALUE {PWR(LIMIT(V(G,K)-{GCO},0,1E6),1.5)*{GCF}}
Gg    G  K  VALUE {(V(GF)+V(LD))}
CM1	G	K	{CGK}
CM2	A	G	{CGA}
CM3	A	K	{CAK}
RF1	A	0	1000MEG
RF2	G	0	1000MEG
RF3	K	0	1000MEG
.ENDS

.SUBCKT 2A3 A G K
XV1 A G K TRIODENH
+PARAMS: LIP= 1.5 LIF= .003 RAF= 1.92357959289845E-03 RAS= .98 CDO= 0 
+ RAP= 0.005 ERP= 1.55 
+ MU0= 4.2 MUR= 0.0006 EMC= 0.0000868 
+ GCO=-0.2 GCF= 0.00001 
+ CGA=1.65E-11 CGK=7.50E-12 CAK=5.50E-12
.ENDS

.SUBCKT 2A3_sofia A G K
+PARAMS: MU=4.545 ERP=1.5
+ KK1=1744 KP=41.4 KVB=17.1 vg0=1.5
+ CGA=16.5p CGK=7.5p CAK=5.5p RGI=1000
.func V_6() {KP*( (1/MU)+((V(G,K)-vg0)/sqrt(V(A,K)**2+KVB**2)) )} 
E8 8 0 VALUE={(V(A,K))/KP*LN(1+EXP(V_6()))}
Eam am 0 VALUE= {2*Pow(V(8),ERP)/KK1}
GA A K VALUE={V(am)}
D3 5 k DX ; FOR GRID CURRENT
R1 g 5 {RGI} ; FOR GRID CURRENT
Rak A K 1G
Rgk G K 1G
C1 G K {CGK}
C2 G A {CGA}
C3 A K {CAK}
.MODEL DX D(IS=1N RS=1)
.ENDS

.SUBCKT 3CX300 A G K
XV1 A G K TRIODENH
+PARAMS: LIP= 1.5 LIF= 10 RAF= 0.00536 RAS= 1 CDO= 0 
+ RAP= 0.005 ERP= 1.25 
+ MU0= 8.321 MUR= 0.0012 EMC= 0.000533 
+ GCO= 0 GCF= 0.0001 
+ CGA=1.00E-11 CGK=2.50E-11 CAK=1.00E-12
.ENDS

.SUBCKT 6AS7_6080 A G K
XV1 A G K TRIODENH
+PARAMS: LIP= 1 LIF= 0.01 RAF= 0.0058 RAS= 0.7 CDO= 0 
+ RAP= 0.035 ERP= 1.5 
+ MU0= 2.05 MUR= 0.0017 EMC= 0.0005 
+ GCO= 0 GCF= 0 
+ CGA=1.10E-11 CGK=8.00E-12 CAK=3.00E-12
.ENDS

.SUBCKT 6BM8 A G K
XV1 A G K TRIODENH
+PARAMS: LIP= 1.5 LIF= 10 RAF= 0.030667 RAS= 5 CDO=-0.5 
+ RAP= 0.587 ERP= 1.5 
+ MU0= 50 MUR= 0.035 EMC= 0.00000256 
+ GCO= 0 GCF= 0 
+ CGA=4.00E-12 CGK=2.70E-12 CAK=4.00E-12
.ENDS

.SUBCKT 6C33C_sofia A G K
+PARAMS: MU=2.67 ERP=1.45
+ KK1=418 KP=14.6 KVB=5
+ CGA=30p CGK=30p CAK=10p RGI=1000
.func V_6() {KP*( (1/MU)+(V(G,K)/sqrt(V(A,K)**2+KVB**2)) )} 
E8 8 0 VALUE={(V(A,K))/KP*LN(1+EXP(V_6()))}
Eam am 0 VALUE= {2*Pow(V(8),ERP)/KK1}
GA A K VALUE={V(am)}
D3 5 k DX ; FOR GRID CURRENT
R1 g 5 {RGI} ; FOR GRID CURRENT
Rak A K 1G
Rgk G K 1G
C1 G K {CGK}
C2 G A {CGA}
C3 A K {CAK}
.MODEL DX D(IS=1N RS=1) 
.ENDS

.SUBCKT 6C4C  A G K
+PARAMS: MU=4.4 ERP=1.5
+ KK1=2136 KP=49.5 KVB=23 vg0=-3
+ CGA=16.5p CGK=7.5p CAK=5.5p RGI=1000   ;(2A3 values)
.func V_6() {KP*( (1/MU)+((V(G,K)-vg0)/sqrt(V(A,K)**2+KVB**2)) )}
E8    8  0  VALUE={(V(A,K))/KP*LN(1+EXP(V_6()))}
Eam   am  0  VALUE= {2*Pow(V(8),ERP)/KK1}
GA    A  K  VALUE={V(am)}
D3    5 k DX     ; FOR GRID CURRENT
R1    g 5 {RGI}  ; FOR GRID CURRENT
Rak   A  K  1G
Rgk   G  K  1G
C1    G  K  {CGK}
C2    G  A  {CGA}
C3    A  K  {CAK}
.MODEL DX D(IS=1N RS=1)
.ENDS

.subckt 6C45-PE 1 2 3 ; plate grid cathode
+ params: mu=47.4501 ex=2.374193 kg1=268.615545 kp=485.735371 kvb=501.503636 rgi=300
+ ccg=2.4p cgp=4p ccp=.7p
e1 7 0 value= {v(1,3)/kp*log(1+exp(kp*(1/mu+v(2,3)/sqrt(kvb+v(1,3)*v(1,3)))))}
re1 7 0 1g
g1 1 3 value= {(pwr(v(7),ex)+pwrs(v(7),ex))/kg1}
rcp 1 3 1g
c1 2 3 {ccg}
c2 1 2 {cgp}
c3 1 3 {ccp}
r1 2 5 {rgi}
d3 5 3 dx
.model dx d(is=1n rs=1 cjo=10pf tt=1n)
.ends

.subckt 6CW4 1 2 3 ; placca griglia catodo NUVISTOR R.C.A.
+ params: mu=68.75 ex=1.35 kg1=160 kp=250 kvb=300 rgi=200
+ ccg=4.1p cgp=.92p ccp=.18p
+ a=2.133e-7 b=-9.40e-5 c=.0139666 d=.64
e1 7 0 value=
+{v(1,3)/kp*log(1+exp(kp*(1/mu+v(2,3)/sqrt(kvb+v(1,3)*v(1,3)))))}
re1 7 0 1g
e2 8 0 value=
+{a*v(1,3)*v(1,3)*v(1,3)+b*v(1,3)*v(1,3)+c*v(1,3)+d}
re2 8 0 1g
g1 1 3 value= {(pwr(v(7),v(8))+pwrs(v(7),v(8)))/kg1}
rcp 1 3 1g
c1 2 3 {ccg}
c2 1 2 {cgp}
c3 1 3 {ccp}
r1 2 5 {rgi}
d3 5 3 dx
.model dx d(is=1n rs=1 cjo=10pf tt=1n)
.ends

.SUBCKT 6DJ8 A G K
XV1 A G K TRIODENH
+PARAMS: LIP= 1.5 LIF= 10 RAF= 0.09 RAS= 0.2 CDO= 0 
+ RAP= 0 ERP= 1.35 
+ MU0= 33 MUR= 0.02 EMC= 0.0000795 
+ GCO=-0.2 GCF= 0 
+ CGA=1.40E-12 CGK=3.30E-12 CAK=1.80E-12
.ENDS

.subckt 6H30  P G K
    Bp  P K  I=(0.3800825583m)*uramp(V(P,K)*ln(1.0+(-0.02540430176)+exp((7.018331616)+(7.018331616)*((15.85848193)+(-66.34009258m)*V(G,K))*V(G,K)/sqrt((27.2125877)**2+(V(P,K)-(5.267363515))**2)))/(7.018331616))**(1.211856956)
.ends 6H30

.subckt 6HV5  P G K
    Bp  P K  I=((0.002251977888m)+(-5.369015936e-005m)*V(G,K))*uramp((370.7812379)*V(G,K)+V(P,K)+(423.2938397))**1.5 * V(P,K)/(V(P,K)+(57.14378617))
.ends

.SUBCKT 6N1P A G K
XV1 A G K TRIODENH
+PARAMS: LIP= 1.5 LIF= 10 RAF= 0.01 RAS= 1 CDO= 0 
+ RAP= 0 ERP= 1.6 
+ MU0= 37.5 MUR= 0.01 EMC= 0.000005 
+ GCO= 0 GCF= 0 
+ CGA=1.60E-12 CGK=3.20E-12 CAK=1.50E-12
.ENDS

.SUBCKT 6SL7GT P G K
E1  2  0  VALUE={V(P,K)+65.5*V(G,K)}
R1  2  0  1.0K
Gp  P  K  VALUE={1.54E-6*(PWR(V(2),1.5)+PWRS(V(2),1.5))/2}
Cgk G  K  3.2P
Cgp G  P  2.8P
Cpk P  K  3.5P
.ENDS

.SUBCKT 6SN7GTB A G K
XV1 A G K TRIODENH
+PARAMS: LIP= 1 LIF= 0.0037 RAF= 0.02 RAS= 2 CDO= 0 
+ RAP= 0.002 ERP= 1.4 
+ MU0= 19.2642 MUR= 0.006167 EMC= 0.0000189 
+ GCO= 0 GCF= 0.000213 
+ CGA=3.90E-12 CGK=2.40E-12 CAK=7.00E-13
.ENDS

.SUBCKT 6SN7_sofia A G K
+PARAMS: MU=21.95 ERP=1.5
+ KK1=2100 KP=169 KVB=4 vg0=-.45
+ CGA=4p CGK=3p CAK=1.2p RGI=1000
.func V_6() {KP*( (1/MU)+((V(G,K)-vg0)/sqrt(V(A,K)**2+KVB**2)) )} 
E8 8 0 VALUE={(V(A,K))/KP*LN(1+EXP(V_6()))}
Eam am 0 VALUE= {2*Pow(V(8),ERP)/KK1}
GA A K VALUE={V(am)}
D3 5 k DX ; FOR GRID CURRENT
R1 g 5 {RGI} ; FOR GRID CURRENT
Rak A K 1G
Rgk G K 1G
C1 G K {CGK}
C2 G A {CGA}
C3 A K {CAK}
.MODEL DX D(IS=1N RS=1)
.ENDS

subckt 12A4  P G K
    Bp  P K  I=(0.04842259598m)*uramp(V(P,K)*ln(1.0+(-0.1171696503)+exp((6.561427624)+(6.561427624)*((18.54552963)+(-100.6055605m)*V(G,K))*V(G,K)/sqrt((40.8808477)**2+(V(P,K)-(25.43292096))**2)))/(6.561427624))**(1.491616235)
.ends

.SUBCKT 12AT7_ECC81 A G K
XV1 A G K TRIODENH
+PARAMS: LIP= 1 LIF= 0.0037 RAF= 0.09869 RAS= 1 CDO=-0.5 
+ RAP= 0.1 ERP= 1.4 
+ MU0= 45.093 MUR= 0.012937 EMC= 0.00000863 
+ GCO=-0.5 GCF= 0.00012 
+ CGA=1.60E-12 CGK=2.30E-12 CAK=4.00E-13
.ENDS

.SUBCKT 12AU7_ECC82 A G K
XV1 A G K TRIODENH
+PARAMS: LIP= 1 LIF= 0.0037 RAF= 0.000001 RAS= 2.065382774 CDO= 0 
+ RAP= 0.18 ERP= 1.4 
+ MU0= 17.08958652 MUR= 0.010938375 EMC= 0.0000183 
+ GCO= 0 GCF= 0.00012 
+ CGA=1.60E-12 CGK=1.80E-12 CAK=4.50E-13
.ENDS

.SUBCKT 12AX7_ECC83 A G K
XV1 A G K TRIODENH
+PARAMS: LIP= 1.5 LIF= 0.000016 RAF= 0.076498 RAS= 1 CDO=-0.53056 
+ RAP= 0.18 ERP= 1.5 
+ MU0= 87.302 MUR=-0.013621 EMC= 0.00000111 
+ GCO=-0.2 GCF= 0.00001 
+ CGA=3.90E-12 CGK=2.40E-12 CAK=7.00E-13
.ENDS

.subckt 12B4  P G K
    Bp  P K  I=(0.7475666979m)*uramp(V(P,K)*ln(1.0+(-0.03869784353)+exp((5.06748961)+(5.06748961)*((7.783573199)+(-7.718521472m)*V(G,K))*V(G,K)/sqrt((16.65965534)**2+(V(P,K)-(1.974437216))**2)))/(5.06748961))**(1.293967904)
.ends 12B4

.SUBCKT 12BH7A P G K
E1  2  0  VALUE={V(P,K)+16.64*V(G,K)}
R1  2  0  1.0K
Gp  P  K  VALUE={22.34E-6*(PWR(V(2),1.5)+PWRS(V(2),1.5))/2}
Cgk G  K  3.2P
Cgp G  P  2.6P
Cpk P  K  0.5P
.ENDS

.SUBCKT 71a  A G K
+PARAMS: MU=3.15 ERP=1.5
+ KK1=6350 KP=26.5 KVB=9 vg0=0.5 va0=6.0
+  CGA=7.4p CGK=3.7p CAK=2.1p RGI=1000
.func V_6() {KP*( (1/MU)+((V(G,K)-vg0)/sqrt(V(A,K)**2+KVB**2)) )}
E8    8  0  VALUE={(V(A,K)-va0)/KP*LN(1+EXP(V_6()))}
Eam   am  0  VALUE= {2*Pow(V(8),ERP)/KK1}
GA    A  K  VALUE={V(am)}
D3    5 k DX     ; FOR GRID CURRENT
R1    g 5 {RGI}  ; FOR GRID CURRENT
Rak   A  K  1G
Rgk   G  K  1G
C1    G  K  {CGK}
C2    G  A  {CGA}
C3    A  K  {CAK}
.MODEL DX D(IS=1N RS=1)
.ENDS

.subckt 75TL  P G K
    Bp  P K  I=((0.008071931767m)+(3.503608694e-005m)*V(G,K))*uramp((11.35872332)*V(G,K)+V(P,K)+(-21.07038254))**1.5 * V(P,K)/(V(P,K)+(-4.024455933))
.ends

.SUBCKT 76 A G K
XV1 A G K TRIODENH
+PARAMS: LIP= 1 LIF= 10 RAF= 0.015 RAS= 1.8 CDO= 0 
+ RAP= 0 ERP= 1.6 
+ MU0= 12.8 MUR= 0.001 EMC= 0.000008 
+ GCO= 0 GCF= 0 
+ CGA=2.80E-12 CGK=3.50E-12 CAK=2.50E-12
.ENDS

.subckt 211_VT4C 1 3 4 ; TRIODO DI POTENZA D.H.T. ( G.E.)
g1 2 4 value = {(exp(1.5*(log((v(2,4)/12)+v(3,4)))))/3010}
c1 3 4 6p
c2 3 1 14.5p
c3 1 4 5.5p
r1 3 5 10k
d1 1 2 dx
d2 4 2 dx2
d3 5 4 dx
.model dx d(is=1p rs=1)
.model dx2 d(is=1n rs=1)
.ends

.SUBCKT GL211  P G K
E1  2  0  VALUE={V(P,K)+12.11*V(G,K)}
R1  2  0  1.0K
Gp  P  K  VALUE={9.39E-6*(PWR(V(2),1.5)+PWRS(V(2),1.5))/2}
Gg  G  K  VALUE={358E-6*(PWR(V(G,K),1.5)+PWRS(V(G,K),1.5))/2}
Cgk G  K  6.0P
Cgp G  P  14.5P
Cpk P  K  5.5P
.ENDS

.SUBCKT 300B A G K
XV1 A G K TRIODENH
+PARAMS: LIP= 1 LIF= 10 RAF= 0.00311 RAS= 1.013608 CDO= 0 
+ RAP= 0 ERP= 1.5 
+ MU0= 3.7992 MUR= 0.000362 EMC= 0.000116 
+ GCO= 0 GCF= 0 
+ CGA=1.50E-11 CGK=9.00E-12 CAK=4.30E-12
.ENDS

.SUBCKT 300B_sofia  A G K
+PARAMS: MU=4.16 ERP=1.5
+ KK1=1922 KP=45.5 KVB=7 vg0=3
+ CGA=15.p CGK=9.p CAK=4.3p RGI=1000
.func V_6() {KP*( (1/MU)+((V(G,K)-vg0)/sqrt(V(A,K)**2+KVB**2)) )}
E8    8  0  VALUE={(V(A,K))/KP*LN(1+EXP(V_6()))}
Eam   am  0  VALUE= {2*Pow(V(8),ERP)/KK1}
GA    A  K  VALUE={V(am)}
D3    5 k DX     ; FOR GRID CURRENT
R1    g 5 {RGI}  ; FOR GRID CURRENT
Rak   A  K  1G
Rgk   G  K  1G
C1    G  K  {CGK}
C2    G  A  {CGA}
C3    A  K  {CAK}
.MODEL DX D(IS=1N RS=1)
.ENDS	

.subckt 437  P G K
    Bp  P K  I=(0.02254655914m)*uramp(V(P,K)*ln(1.0+(-0.4880850946)+exp((0.9206824464)+(0.9206824464)*((62.11491976)+(-2109.77701m)*V(G,K))*V(G,K)/sqrt((52.5190469)**2+(V(P,K)-(21.20975915))**2)))/(0.9206824464))**(1.712612552)
.ends

.SUBCKT SV572_3 A G K
XV1 A G K TRIODENH
+PARAMS: LIP= 1 LIF= 0.0018 RAF= 0.0012 RAS= 0.5 CDO= 0 
+ RAP= 0 ERP= 1.4 
+ MU0= 3.79928 MUR= 0.0002 EMC= 0.0000425 
+ GCO= 0 GCF= 0.0000349 
+ CGA=4.00E-12 CGK=4.00E-12 CAK=1.00E-12
.ENDS

.SUBCKT SV572_10 A G K
XV1 A G K TRIODENH
+PARAMS: LIP= 1.4 LIF= 0.0008 RAF= 0.001 RAS= 1 CDO= 0 
+ RAP=-0.00117 ERP= 1.38 
+ MU0= 10 MUR= 0.0001 EMC= 0.0000272 
+ GCO=-0.2 GCF= 0.0003 
+ CGA=5.00E-12 CGK=6.40E-12 CAK=1.00E-12
.ENDS

.subckt 801a 1 2 3
+ params: mu=8.06 ex=1.596 kg1=11520 kp=162 kvb=0.1 rgi=950
+ ccg=4.5p cgp=6p ccp=1.5p
e1 7 0 value=
+{v(1,3)/kp*log(1+exp(kp*(1/mu+v(2,3)/sqrt(kvb+v(1,3)*v(1,3)))))}
re1 7 0 1g
g1 1 3 value= {(pwr(v(7),ex)+pwrs(v(7),ex))/kg1}
rcp 1 3 1g
c1 2 3 {ccg}
c2 1 2 {cgp}
c3 1 3 {ccp}
r1 2 5 {rgi}
d3 5 3 dx
.model dx d(is=1n rs=1 cjo=1pf tt=1n)
.ends


.subckt 811a 1 2 3 ;
+ params: mu=160 ex=1.317 kg1=1350 kp=100 kvb=1400 rgi=1000
+ ccg=2.3p cgp=2.4p ccp=.9p
+ a=1.6667e-10 b=-.0000002875 c=0.0001758333 d=1.275 
e1 7 0 value=
+{v(1,3)/kp*log(1+exp(kp*(1/mu+v(2,3)/sqrt(kvb+v(1,3)*v(1,3)))))}
re1 7 0 1g
e2 8 0 value=
+{a*v(1,3)*v(1,3)*v(1,3)+b*v(1,3)*v(1,3)+c*v(1,3)+d}
re2 8 0 1g
e3 9 0 table {v(2,3)} =
+ (-1 1.1e-16)
+ (0, .6e-4) (20, 5.38e-4)
+ (40, 6.25e-4) (60, 7.41e-4)
re3 9 0 1g
g1 1 3 value= {(pwr(v(7),v(8))+pwrs(v(7),v(8)))*v(9)}
rcp 1 3 100k
c1 2 3 {ccg}
c2 1 2 {cgp}
c3 1 3 {ccp}
r1 2 5 {rgi}
d3 5 3 dx
.model dx d(is=1n rs=1 cjo=10pf tt=1n)
.ends

.SUBCKT 845  A G K
+PARAMS: MU=5.355 ERP=1.5
+ KK1=6323  KP=85.64  KVB=65.8 vg0=3 va0=0
+ CGA=13.5E-12 CGK=6E-12 CAK=6.5E-12 RGI=1000;
.func V_6() {KP*( (1/MU)+((V(G,K)-vg0)/sqrt(V(A,K)**2+KVB**2)) )}
E8    8  0  VALUE={(V(A,K)-va0)/KP*LN(1+EXP(V_6()))}
Eam   am  0  VALUE= {2*Pow(V(8),ERP)/KK1}
GA    A  K  VALUE={V(am)}
D3    5 k DX     ; FOR GRID CURRENT
R1    g 5 {RGI}  ; FOR GRID CURRENT
Rak   A  K  1G
Rgk   G  K  1G
C1    G  K  {CGK}
C2    G  A  {CGA}
C3    A  K  {CAK}
.MODEL DX D(IS=1N RS=1)
.ENDS

.SUBCKT 5687wa  A G K
+PARAMS: MU=18.14 ERP=1.48
+ KK1=665 KP=128.5 KVB=13.6 vg0=-0.7
+ CGA=5.2p CGK=5.2p CAK=0.8p RGI=1000
.func V_6() {KP*( (1/MU)+((V(G,K)-vg0)/sqrt(V(A,K)**2+KVB**2)) )}
E8    8  0  VALUE={(V(A,K))/KP*LN(1+EXP(V_6()))}
Eam   am  0  VALUE= {2*Pow(V(8),ERP)/KK1}
GA    A  K  VALUE={V(am)}
D3    5 k DX     ; FOR GRID CURRENT
R1    g 5 {RGI}  ; FOR GRID CURRENT
Rak   A  K  1G
Rgk   G  K  1G
C1   G  K  {CGK}
C2   G  A  {CGA}
C3   A  K  {CAK}
.MODEL DX D(IS=1N RS=1)
.ENDS		

.SUBCKT 5751 A G K
XV1 A G K TRIODENH
+PARAMS: LIP= 1.5 LIF= 0.000016 RAF= 0.075772 RAS= 1 CDO=-0.53056 
+ RAP= 0.131285 ERP= 1.5 
+ MU0= 62.94685 MUR=-0.0111 EMC= 0.00000142 
+ GCO=-0.2 GCF= 0.00001 
+ CGA=1.40E-12 CGK=1.40E-12 CAK=4.50E-13
.ENDS

.subckt 5842_417 1 2 3
+ params: mu=42.4 ex=2.21 kg1=393 kp=629 kvb=446 rgi=2000
+ ccg=9p cgp=1.8p ccp=.48p
e1 7 0 value=
+{v(1,3)/kp*log(1+exp(kp*(1/mu+v(2,3)/sqrt(kvb+v(1,3)*v(1,3)))))}
re1 7 0 1g
g1 1 3 value= {(pwr(v(7),ex)+pwrs(v(7),ex))/kg1}
rcp 1 3 1g
c1 2 3 {ccg}
c2 1 2 {cgp}
c3 1 3 {ccp}
r1 2 5 {rgi}
d3 5 3 dx
.model dx d(is=1n rs=1 cjo=10pf tt=1n)
.ends

.subckt 7119  P G K
    Bp  P K  I=(0.2025738143m)*uramp(V(P,K)*ln(1.0+(0.04163079423)+exp((3.21147579)+(3.21147579)*((23.87181902)+(-454.0996836m)*V(G,K))*V(G,K)/sqrt((33.58240995)**2+(V(P,K)-(16.01952758))**2)))/(3.21147579))**(1.235675486)
.ends

.SUBCKT D3a_7721 A G K
XV1 A G K TRIODENH
+PARAMS: LIP= 1 LIF= 1E-3 RAF= 240E-3 RAS= 2 CDO= -0.16
+ RAP= 8E-3 ERP= 1.5
+ MU0= 70 MUR= 1.5E-3 EMC= 4.7E-5
+ GCO= -0.16 GCF= 213E-6
+CGA= 2.7E-12 CGK= 7.3E-12 CAK=3.1E-12
.ENDS

.subckt E182CC 1 2 3 ; placca griglia catodo
+ params: mu=24 ex=1.7 kg1=75 kp=320 kvb=300 rgi=2k
+ ccg=2.3p cgp=2.4p ccp=.9p
e1 7 0 value=
+{v(1,3)/kp*log(1+exp(kp*(1/mu+v(2,3)/sqrt(kvb+v(1,3)*v(1,3)))))}
re1 7 0 1g
g1 1 3 value= {(pwr(v(7),ex)+pwrs(v(7),ex))/kg1}
rcp 1 3 1g
c1 2 3 {ccg}
c2 1 2 {cgp}
c3 1 3 {ccp}
r1 2 5 {rgi}
d3 5 3 dx
.model dx d(is=1n rs=1 cjo=10pf tt=1n)
.ends

.subckt E88CC_6922 1 3 4 ; TRIODO DI SEGNALE  (SQ PHILIPS) *modello sperimentale*
g1 2 4 value = {(exp(1.5*(log((v(2,4)/(-0.1369*v(3,4)*v(3,4)-1.0232*v(3,4)+31.5035))+v(3,4)))))/120}
c1 3 4 3.3p
c2 3 1 1.4p
c3 1 4 2.8p
r1 3 5 10k
d1 1 2 dx
d2 4 2 dx2
d3 5 4 dx
.model dx d(is=1p rs=1)
.model dx2 d(is=1n rs=1)
.ends ; eq. 6dj8, 7dj8, 6922

.SUBCKT ECC99 A G K 
+PARAMS: MU=23.33 ERP=1.48
+ KK1=448.5 KP=172.65 KVB=8.92
+ CGA=5p CGK=6p CAK=1p RGI=1000
.func V_6() {KP*( (1/MU)+(V(G,K)/sqrt(V(A,K)**2+KVB**2)) )} 
E8 8 0 VALUE={(V(A,K))/KP*LN(1+EXP(V_6()))}
Eam am 0 VALUE= {2*Pow(V(8),ERP)/KK1}
GA A K VALUE={V(am)}
D3 5 k DX ; FOR GRID CURRENT
R1 g 5 {RGI} ; FOR GRID CURRENT
Rak A K 1G
Rgk G K 1G
C1 G K {CGK}
C2 G A {CGA}
C3 A K {CAK}
.MODEL DX D(IS=1N RS=1)
.ENDS

.SUBCKT EL34_sofia A G K
+PARAMS: MU=11.17 ERP=1.48
+ KK1=597 KP=40.5 KVB=24.6 vg0=-.4
+ CGA=1.1p CGK=15p CAK=8.5p RGI=1000
.func V_6() {KP*( (1/MU)+((V(G,K)-vg0)/sqrt(V(A,K)**2+KVB**2)) )} 
E8 8 0 VALUE={(V(A,K))/KP*LN(1+EXP(V_6()))}
Eam am 0 VALUE= {2*Pow(V(8),ERP)/KK1}
GA A K VALUE={V(am)}
D3 5 k DX ; FOR GRID CURRENT
R1 g 5 {RGI} ; FOR GRID CURRENT
Rak A K 1G
Rgk G K 1G
C1 G K {CGK}
C2 G A {CGA}
C3 A K {CAK}
.MODEL DX D(IS=1N RS=1)
.ENDS

.SUBCKT GM70  A G K
+PARAMS: MU=8.037 ERP=1.5
+ KK1=4121 KP=182.25 KVB=34 vg0=-5.7
+ CGA=12.p CGK=8.p CAK=4.p RGI=1000
.func   V_6() {KP*( (1/MU)+((V(G,K)-vg0)/sqrt(V(A,K)**2+KVB**2)) )}
E8    8  0  VALUE={(V(A,K))/KP*LN(1+EXP(V_6()))}
Eam   am  0  VALUE= {2*Pow(V(8),ERP)/KK1}
GA    A  K  VALUE={V(am)}
D3    5 k DX     ; FOR GRID CURRENT
R1    g 5 {RGI}  ; FOR GRID CURRENT
Rak   A  K  1G
Rgk   G  K  1G
C1    G  K  {CGK}
C2    G  A  {CGA}
C3    A  K  {CAK}
.MODEL DX D(IS=1N RS=1)
.ENDS

.SUBCKT VV30B_sofia  A G K
+PARAMS: MU=3.775 ERP=1.52
+ KK1=2230 KP=43.6 KVB=5 vg0=4
+ CGA=15.p CGK=9.p CAK=4.3p RGI=1000 ; 300b cap values
.func V_6() {KP*( (1/MU)+((V(G,K)-vg0)/sqrt(V(A,K)**2+KVB**2)) )}
E8    8  0  VALUE={(V(A,K))/KP*LN(1+EXP(V_6()))}
Eam   am  0  VALUE= {2*Pow(V(8),ERP)/KK1}
GA    A  K  VALUE={V(am)}
D3    5 k DX     ; FOR GRID CURRENT
R1    g 5 {RGI}  ; FOR GRID CURRENT
Rak   A  K  1G
Rgk   G  K  1G
C1    G  K  {CGK}
C2    G  A  {CGA}
C3    A  K  {CAK}
.MODEL DX D(IS=1N RS=1)
.ENDS	



